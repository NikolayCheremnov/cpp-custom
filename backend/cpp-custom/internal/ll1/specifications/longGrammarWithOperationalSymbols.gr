<программа> -> <глобальное_описание> <программа> | EPSILON
<глобальное_описание> -> <описание_процедуры> | <описание> | ;
<описание_процедуры> -> void <описание_процедуры_(1)> #epd
<описание_процедуры_(1)> -> IDENTITY #pi ( <описание_параметров> ) <составной_оператор>
<описание_параметров> -> <тип> IDENTITY #pad <описание_параметров_1> | EPSILON
<описание_параметров_1> -> , <описание_параметров> | EPSILON
<описание> -> <переменные> #evd ; | <константы> #encd ;
<переменные> -> <первая_переменная> <переменные_1>
<переменные_1> -> , <переменная> <переменные_1> | EPSILON
<первая_переменная> -> <тип> IDENTITY #fvd <первая_переменная_1>
<первая_переменная_1> -> = #se <выражение> #eewd | EPSILON
<переменная> -> IDENTITY #vd <переменная_1>
<переменная_1> -> = #se <выражение> #eewd | EPSILON
<константы> -> <первая_константа> <константы_1>
<константы_1> -> , <const> <константы_1> | EPSILON
<первая_константа> -> const <тип> IDENTITY #fncd = #se <выражение> #eewd
<const> -> IDENTITY #ncd = #se <выражение> #eewd
<тип> -> int | short int | long int | bool
<составной_оператор> -> { #sco <операторы_и_описания> #eco }
<операторы_и_описания> -> <оператор> <операторы_и_описания> | <описание> <операторы_и_описания> | EPSILON
<оператор> -> IDENTITY <оператор_идентификатор> ; | <составной_оператор> | <for> | return ; | ;
<оператор_идентификатор> -> #sa = #se <выражение> #eewa | #spc ( <вид_параметров> ) #epc
<вид_параметров> -> <параметры> | EPSILON
<параметры> -> IDENTITY #pvat <параметры_1> | CONSTANT #pcat <параметры_1>
<параметры_1> -> , <параметры> | EPSILON
<for> -> #sfl for ( <оператор_for_1> ; <выражение_for> ; <присваивание_for> ) <оператор> #efl
<оператор_for_1> -> <тип> IDENTITY #flcd = #se <выражение> #eewdfi | IDENTITY #sa = #se <выражение> #eewafi | EPSILON
<выражение_for> -> #se <выражение> #eewc | EPSILON
<присваивание_for> -> IDENTITY #sa = #se <выражение> #eewaf | EPSILON
<выражение> -> + <слагаемое> <выражение_(1)> | - <слагаемое> <выражение_(1)> | <слагаемое> <выражение_(1)>
<выражение_(1)> -> == <слагаемое> <выражение_(1)> | != <слагаемое> <выражение_(1)> | <= <слагаемое> <выражение_(1)> | >= <слагаемое> <выражение_(1)> | < <слагаемое> <выражение_(1)> | > <слагаемое> <выражение_(1)> | EPSILON
<слагаемое> -> <множитель> <слагаемое_(1)>
<слагаемое_(1)> -> + <множитель> <слагаемое_(1)> | - <множитель> <слагаемое_(1)> | EPSILON
<множитель> -> <эл.выр.> <множитель_(1)>
<множитель_(1)> -> * <эл.выр.> <множитель_(1)> | / <эл.выр.> <множитель_(1)> | % <эл.выр.> <множитель_(1)> | EPSILON
<эл.выр.> -> IDENTITY | CONSTANT | ( <выражение> )