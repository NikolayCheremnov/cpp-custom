<программа> -> <глобальное_описание> <программа> | EPSILON
<глобальное_описание> -> <описание_процедуры> | <описание> | ;
<описание_процедуры> -> void <описание_процедуры_(1)>
<описание_процедуры_(1)> -> IDENTITY ( <описание_параметров> ) <составной_оператор>
<описание_параметров> -> <тип> IDENTITY <описание_параметров_1> | EPSILON
<описание_параметров_1> -> , <описание_параметров> | EPSILON
<описание> -> <переменные> ; | <константы> ;
<переменные> -> <первая_переменная> <переменные_1>
<переменные_1> -> , <переменная> <переменные_1> | EPSILON
<первая_переменная> -> <тип> IDENTITY <первая_переменная_1>
<первая_переменная_1> -> = <выражение> | EPSILON
<переменная> -> IDENTITY <переменная_1>
<переменная_1> -> = <выражение> | EPSILON
<константы> -> <первая_константа> <константы_1>
<константы_1> -> , <const> <константы_1> | EPSILON
<первая_константа> -> const <тип> IDENTITY = <выражение>
<const> -> IDENTITY = <выражение>
<тип> -> int | short int | long int | bool
<составной_оператор> -> { <операторы_и_описания> }
<операторы_и_описания> -> <оператор> <операторы_и_описания> | <описание> <операторы_и_описания> | EPSILON
<оператор> -> IDENTITY <оператор_идентификатор> ; | <составной_оператор> | <for> | return ; | ;
<оператор_идентификатор> ->  = <выражение> | ( <вид_параметров> )
<вид_параметров> -> <параметры> | EPSILON
<параметры> -> IDENTITY <параметры_1> | CONSTANT <параметры_1>
<параметры_1> -> , <параметры> | EPSILON
<for> -> for ( <оператор_for_1> ; <выражение_for> ; <присваивание_for> ) <оператор>
<оператор_for_1> -> <тип> IDENTITY = <выражение> | IDENTITY = <выражение> | EPSILON
<выражение_for> -> <выражение> | EPSILON
<присваивание_for> -> IDENTITY = <выражение> | EPSILON
<выражение> -> + <слагаемое> <выражение_(1)> | - <слагаемое> <выражение_(1)> | <слагаемое> <выражение_(1)>
<выражение_(1)> -> == <слагаемое> <выражение_(1)> | != <слагаемое> <выражение_(1)> | <= <слагаемое> <выражение_(1)> | >= <слагаемое> <выражение_(1)> | < <слагаемое> <выражение_(1)> | > <слагаемое> <выражение_(1)> | EPSILON
<слагаемое> -> <множитель> <слагаемое_(1)>
<слагаемое_(1)> -> + <множитель> <слагаемое_(1)> | - <множитель> <слагаемое_(1)> | EPSILON
<множитель> -> <эл.выр.> <множитель_(1)>
<множитель_(1)> -> * <эл.выр.> <множитель_(1)> | / <эл.выр.> <множитель_(1)> | % <эл.выр.> <множитель_(1)> | EPSILON
<эл.выр.> -> IDENTITY | CONSTANT | ( <выражение> )